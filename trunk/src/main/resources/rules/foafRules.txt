[(?indi p:SEX  ?sex )                    (?sex  rdfs:label ?label)->(?indi    http://xmlns.com/foaf/0.1/gender            ?label  )]

[(?indi p:NAME ?name)(?name p:last ?last)(?last rdfs:label ?label)->(?indi    http://xmlns.com/foaf/0.1/familyName        ?label  )]

[(?indi p:NAME ?name)(?name p:givn ?givn)(?givn rdfs:label ?label)->(?indi    http://xmlns.com/foaf/0.1/givenName         ?label  )]

[(?indi p:NAME ?name)                    (?name rdfs:label ?label)
                       regex(?label, '(.*)/(.*)/(.*)', ?f, ?l, ?t)
                       strConcat(?f, " ", ?l, ?name)              ->(?indi    http://xmlns.com/foaf/0.1/name              ?name   )]
[(?fam  p:WIFE ?wife   )(?fam p:HUSB ?husband)(?fam p:MARR ?m)    ->(?wife    http://purl.org/vocab/relationship/spouseOf ?husband)
                                                                    (?husband http://purl.org/vocab/relationship/spouseOf ?wife   )]

[(?fam  p:WIFE ?wife   )(?fam p:CHIL ?child)                      ->(?child   http://purl.org/vocab/bio/0.1/mother        ?wife   )
                                                                    (?wife    http://purl.org/vocab/bio/0.1/child         ?child  )]
[(?fam  p:HUSB ?husband)(?fam p:CHIL ?child)                      ->(?child   http://purl.org/vocab/bio/0.1/father        ?husband)
                                                                    (?husband http://purl.org/vocab/bio/0.1/child         ?child  )]

[(?indi p:BIRT ?birth)                                            ->(?indi    http://purl.org/vocab/bio/0.1/event         ?birth  )
                                                                    (?birth   rdf:type http://purl.org/vocab/bio/0.1/Birth)
                                                                    (?birth   http://purl.org/vocab/bio/0.1/principal     ?indi   )]

[(?indi p:BIRT ?birth)(?birth p:DATE ?d)(?d rdfs:label ?label)    ->(?birth   http://purl.org/vocab/bio/0.1/date          ?label  )]

[(?indi p:BIRT ?birth)(?birth p:PLAC ?p)(?p rdfs:label ?label)    ->(?birth   http://purl.org/vocab/bio/0.1/place         ?label  )]

