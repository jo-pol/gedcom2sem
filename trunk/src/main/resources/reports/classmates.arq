##################################################################################################
# Copyright 2012, J. Pol 
# This file is part of free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation.
# This package is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
# See the GNU General Public License for more details. A copy of the GNU General Public License is available at <http://www.gnu.org/licenses/>.
##################################################################################################
#
# potential (junior) classmates that achieved a military rank
#
# Apply geoNames.rules, don't drop original statements, provide geoNamesCache*.
#
#########################################################################

### from external data
PREFIX dbo:          <http://dbpedia.org/ontology/>
PREFIX dbr:          <http://dbpedia.org/resource/>
PREFIX dbp:          <http://dbpedia.org/property/>
PREFIX rdf:          <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs:         <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX xsd:          <http://www.w3.org/2001/XMLSchema#> 
PREFIX wgs84:    <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX gn:           <http://www.geonames.org/ontology#>
PREFIX foaf:         <http://xmlns.com/foaf/0.1/>
PREFIX bio:          <http://purl.org/vocab/bio/0.1/>

### converted gedcom
PREFIX t: <http://purl.org/vocab/vnd/gedcom2sem.googlecode.com/2013-01-13/GedcomTags/type#> 
PREFIX r: <http://purl.org/vocab/vnd/gedcom2sem.googlecode.com/2013-01-13/GedcomTags/rule#> 
PREFIX p: <http://purl.org/vocab/vnd/gedcom2sem.googlecode.com/2013-01-13/GedcomTags/predicate#> 

### language extensions
PREFIX  fn: <http://www.w3.org/2005/xpath-functions#> 
PREFIX afn: <http://jena.hpl.hp.com/ARQ/function#> 
PREFIX apf: <http://jena.hpl.hp.com/ARQ/property#> 

SELECT DISTINCT 
  ?placeLiteral
  ?name
  (str(xsd:date(?birthDate)) as ?birth_date)
  (?dbpName                  as ?potential_classmate)
  (str(?dbpDate)             as ?birth_date_classmate)
{
  ### our person from the gedcom
  _:indi p:BIRT [p:DATE [rdfs:label ?birthDate]
                ;p:PLAC [rdfs:label ?placeLiteral]
                ;bio:place ?gn
                ];
         p:NAME [rdfs:label ?name].

  ### just places, no countries!
  FILTER ( ! fn:starts-with(?placeLiteral,", ,") ) 

  ### match our person with downloaded data
  ?gn rdfs:seeAlso ?dbp.

  ### prepare for date evaluation
  LET( ?year := xsd:int ( fn:substring(str(?birthDate), 1, 4) ))
  LET( ?min  := xsd:date( fn:concat   (str(?year-1),"-01-01") ))
  LET( ?max  := xsd:date( fn:concat   (str(?year+2),"-01-01") ))

  ### so far our own data, go explore the web 
  SERVICE <http://dbpedia.org/sparql>
  { 
     # for big cities we need some restriction
     # before applying the (expensive) filter
     ?classmate dbp:rank       _:rank.

     # match with potential classmates
     ?classmate dbo:birthPlace ?dbp;
                foaf:name      ?dbpName;
                dbo:birthDate  ?dbpDate.
     FILTER( ?dbpDate >= ?min && ?dbpDate < ?max )
  }
}
ORDER BY ?name
